(* ************************************************************** *)
(*                                                                *)
(*                      ChPT3                                     *)
(*                                                                *)
(* ************************************************************** *)

(* 
   Author:              F.Orellana 1999

   Mathematica Version: 3.0 

   Requirements:        FeynCalc > 3, Phi 

   Summary:             Configuration file for the family of
                        Lagrangians ChPT3 for Phi

   Description:         Definitions for standard ChPT lagrangians.
        
                        The definitions follow
                        J.F. Donoghue, E. Golowich
                        and B.R. Holstein (1992), "Dynamics of
                        the Standard Model", Cambridge
*)

(* ************************************************************** *)

(* Modified box definitions for FeynCalc *)

<<"FCBoxes.m"

(* ************************************************************** *)

(* SU(3) meson matrix PhiMeson instead of Pion *)

renselectseq[a___RenormalizationState, b___RenormalizationScheme, 
      c___ExpansionState, (___List | ___Rule)] := Sequence[a, b, c];
      
MM[x_?(!NumberQ[#]&),opts___]:=
UFieldMatrix[fcqf[(Particle[PhiMeson,renselectseq[opts]])][x],##]&@@
    Union[OptionsSelect[UFieldMatrix,opts],OptionsSelect[UMatrix,opts]];

MM[ii_?NumberQ,x_,opts___]:=
UFieldMatrix[ii,fcqf[(Particle[PhiMeson,renselectseq[opts]])][x],##]&@@
    Union[OptionsSelect[UFieldMatrix,opts],OptionsSelect[UMatrix,opts]];
    
SMM[x_,opts___]:=MM[1/2,x,opts];

MMS[x_,opts___]:=
UFieldMatrixSeries[fcqf[(Particle[PhiMeson,renselectseq[opts]])][x],##]&@@
    Union[OptionsSelect[UFieldMatrix,opts],OptionsSelect[UMatrix,opts]];
        
(* ************************************************************** *)

(* Usage definitions *)

HighEnergyPhysics`Phi`Objects`ChPT3::"usage"=
"ULagrangian[ChPT3[2]] is the pionic Chiral Perturbation Theory 
lagrangian written in terms of a matrix representation 
of the meson fields to second order in the energy.
ULagrangian[ChPT3[4]] is the pionic Chiral Perturbation Theory lagrangian 
to four'th order in the energy (first written down by Gasser and 
Leutwyler).  To evaluate use ArgumentsSupply";

MM::"usage"="MM[x] := 
UFieldMatrix[QuantumField[Particle[PhiMeson]][x]].  
MM takes two optional arguments with head RenormalizationState 
and RenormalizationScheme respectively";

(* ************************************************************** *)

(* Attributes and data types *)

SetAttributes[ChPT3, NumericFunction];

(* ************************************************************** *)

(* Source fields *)

(* Strip off options passed to Particle[PseudoScalar[0],___]*)

Particle[PseudoScalar[0],o___,__Rule]:=Particle[PseudoScalar[0],o];

(* The scalar source is set to the quark mass matrix *)

QuantumField[Particle[Scalar[0],st___RenormalizationState,
sc___RenormalizationScheme,qs___ExpansionState,
opts___Rule|opts___List],SUNIndex[0]][x_]:=
UQuarkMassMatrix[st,sc,qs,opts];

(* Sources are set to zero *)

IsoVector[QuantumField[Particle[Scalar[0],___],___],___][_]:=0;
(*IsoVector[QuantumField[Particle[PseudoScalar[0],___],___],___][_]:=0;*)
(*IsoVector[QuantumField[Particle[Vector[0],___],___],___][_]:=0;*)
(*IsoVector[QuantumField[Particle[AxialVector[0],___],___],___][_]:=0;*)

(*We don't need the singlet field*)
QuantumField[___,Particle[PseudoScalar[0],___],___,SUNIndex[0],___][_]:=0;
QuantumField[___,Particle[Vector[0],___],___,SUNIndex[0],___][_]:=0;
QuantumField[___,Particle[AxialVector[0],___],___,SUNIndex[0],___][_]:=0;
                        
(* ************************************************************** *)

(* Redefinition of the left and right components
   Ecker, Kambor and Wyler have them the other way round.
   Also its necessary in order to not have the AxialVector (Vector)
   part set to zero *)

IsoVector[QuantumField[aa___, Particle[LeftComponent[a_],i___],
bb___],opts___][x_]:=1/2*(
IsoVector[QuantumField[aa, Particle[Vector[a],i],bb],opts][x]-
IsoVector[QuantumField[aa, Particle[AxialVector[a],i],bb],opts][x]);

IsoVector[QuantumField[aa___, Particle[RightComponent[a_],i___],bb___],
opts___][x_]:=1/2*(
  IsoVector[QuantumField[aa, Particle[Vector[a],i],bb],opts][x]+
  IsoVector[QuantumField[aa, Particle[AxialVector[a],i],bb],opts][x]);

QuantumField[aa___, Particle[LeftComponent[a_],i___],bb___][x_]:=
  1/2*(QuantumField[aa, Particle[Vector[a],i],bb][x]-
        QuantumField[aa, Particle[AxialVector[a],i],bb][x]);

QuantumField[aa___, Particle[RightComponent[a_],i___],bb___][x_]:=
  1/2*(QuantumField[aa, Particle[Vector[a],i],bb][x]+
        QuantumField[aa, Particle[AxialVector[a],i],bb][x]);

(* ************************************************************** *)

(* Adjoints and conjugates *)

Adjoint[p:QuantumField[___,Particle[
(Vector|AxialVector|Scalar|PseudoScalar)[0|1|2],___],___][_]] = p;

Adjoint[p:QuantumField[___,Particle[
(Vector|AxialVector|Scalar|PseudoScalar)[0|1|2],___],___]] = p;

Unprotect[Conjugate];

Conjugate[p:QuantumField[___,Particle[
(Vector|AxialVector|Scalar|PseudoScalar)[0|1|2],___],___][_]] = p;

Conjugate[p:QuantumField[___,Particle[
(Vector|AxialVector|Scalar|PseudoScalar)[0|1|2],___],___]] = p;

Protect[Conjugate];

(* ************************************************************** *)

(* Antiparticles *)

ChargeConjugate[Pion] = Pion;
ChargeConjugate[Vector[0]] = Vector[0];
ChargeConjugate[AxialVector[0]] = AxialVector[0];
ChargeConjugate[Scalar[0]] = Scalar[0];
ChargeConjugate[Scalar[1]] = Scalar[1];
ChargeConjugate[PseudoScalar[0]] = PseudoScalar[0];
ChargeConjugate[PseudoScalar[1]] = PseudoScalar[1];
ChargeConjugate[PionPlus] = PionMinus;
ChargeConjugate[PionMinus] = PionPlus;
ChargeConjugate[PionZero] = PionZero;
ChargeConjugate[KaonMinus] = KaonPlus;
ChargeConjugate[KaonPlus] = KaonMinus;
ChargeConjugate[KaonZero] = KaonZeroBar;
ChargeConjugate[KaonZeroBar] = KaonZero;
ChargeConjugate[EtaMeson] = EtaMeson;

(* ************************************************************** *)

(* Constants *)

(* When calculating infrared divergences, the photon mass
   should not be set to zero *)

ParticleMass[Photon,___]:=0;
ParticleMass[Photon[0],___]:=0;

(*UCouplingConstant[ChPT3[4],1,___]=0;
UCouplingConstant[ChPT3[4],2,___]=0;
UCouplingConstant[ChPT3[4],3,___]=0;
UCouplingConstant[ChPT3[4],4,___]=0;
UCouplingConstant[ChPT3[4],5,___]=0;
UCouplingConstant[ChPT3[4],6,___]=0;
UCouplingConstant[ChPT3[4],7,___]=0;
UCouplingConstant[ChPT3[4],8,___]=0;
UCouplingConstant[ChPT3[4],9,___]=0;
UCouplingConstant[ChPT3[4],10,___]=0;
UCouplingConstant[ChPT3[4],11,___]=0;
UCouplingConstant[ChPT3[4],12,___]=0;*)

(* ************************************************************** *)

(* The covariant derivative *)

CovariantFieldDerivative[aa_,x_,{loris__},rest___] :=
CovariantFieldDerivative[aa,x,##,rest]&@@
HighEnergyPhysics`FeynCalc`LorentzIndex`LorentzIndex/@{loris};

(* -------------------------------------------------------------- *)

(* Standard ChPT - Donoghue, Golowich, Holstein *)

CovariantFieldDerivative[aa_,x_,
lori_HighEnergyPhysics`FeynCalc`LorentzIndex`LorentzIndex,
ar___RenormalizationState,br___RenormalizationScheme,
cr___ExpansionState,(opts___Rule|opts___List)] :=

FieldDerivative[aa,x,lori] +

I*NM[UGeneratorMatrixIsoDotFull[QuantumField[Particle[
LeftComponent[0],ar,br],lori][x],opts],aa] -

I*NM[aa,UGeneratorMatrixIsoDotFull[QuantumField[Particle[
RightComponent[0],ar,br],lori][x],opts]];

(* ************************************************************** *)

(* Re-setting of options and $-variables *)

SetOptions[Renormalize,GaugeGroup->3];
SetOptions[WriteOutUMatrices,GaugeGroup->3];
SetOptions[WriteOutIsoVectors,GaugeGroup->3];
SetOptions[UQuarkMass,GaugeGroup->3,DiagonalToU->True];
SetOptions[UMatrix,GaugeGroup->3];
SetOptions[IsoVector,GaugeGroup->3];
SetOptions[UGenerator,GaugeGroup->3];
SetOptions[UTrace,GaugeGroup->3];
SetOptions[SUNReduce,GaugeGroup->3];
SetOptions[AmplitudeProjection,GaugeGroup->3];
SetOptions[ExpandU,GaugeGroup->3];
SetOptions[FAToFC,EqualMasses->False];
SetOptions[DiscardTerms,
Retain->{Particle[PhiMeson,RenormalizationState[0]]->4}];
SetOptions[CovariantFieldDerivative,GaugeGroup->3];

$UParticlesInUse=
{PhiMeson,Pion,Kaon,KaonPlus,KaonMinus,
KaonZero,KaonZeroBar,EtaMeson,
Photon,Vector[0],AxialVector[0],
PseudoScalar[0]};

$ExpansionQuantities=
{Global`FAFourVector[__],ParticleMass[Pion,a___],
ParticleMass[PhiMeson,b___],ParticleMass[KaonZero,c___],
ParticleMass[KaonPlus,d___],ParticleMass[EtaMeson,d___],
UCouplingConstant[QED[2],e___]};
    
$PropagatorMassesStates={
    PhiMeson[0]->{RenormalizationState[0]},
    Pion[0]->{RenormalizationState[0]},
    Kaon[0]->{RenormalizationState[0]},
    KaonPlus[0]->{RenormalizationState[0]},
    KaonMinus[0]->{RenormalizationState[0]},
    KaonZero[0]->{RenormalizationState[0]},
    KaonZeroBar[0]->{RenormalizationState[0]},
    EtaMeson[0]->{RenormalizationState[0]},
    AxialVector[0][0]->{RenormalizationState[0]},
    PseudoScalar[0][0]->{RenormalizationState[0]},
    Vector[0]->{RenormalizationState[0]},
    Photon[0]->{RenormalizationState[0]}};

$CouplingIsoIndicesSpecifications =
   {PhiMeson[0]->{{IsoRange->{1,2,3,4,5,6,7,8},IsoIndicesString->"I"}},
    PseudoScalar[0][0]->{{IsoRange->{1,2,3,4,5,6,7,8},IsoIndicesString->"I"}},
    AxialVector[0][0]->{{IsoRange->{1,2,3,4,5,6,7,8},IsoIndicesString->"I"}},
    Vector[0][0]->{{IsoRange->{1,2,3,4,5,6,7,8},IsoIndicesString->"I"}}};
   
$MixingFields=
   {};
   
$InsertOnly=
   {Vector[0][0]->{
   HighEnergyPhysics`FeynArts`Incoming,
   HighEnergyPhysics`FeynArts`Outgoing,
   HighEnergyPhysics`FeynArts`External},
   PseudoScalar[0][0]->{
   HighEnergyPhysics`FeynArts`Incoming,
   HighEnergyPhysics`FeynArts`Outgoing,
   HighEnergyPhysics`FeynArts`External},
   AxialVector[0][0]->{
   HighEnergyPhysics`FeynArts`Incoming,
   HighEnergyPhysics`FeynArts`Outgoing,
   HighEnergyPhysics`FeynArts`External}(*,
   Photon[0]->{
   HighEnergyPhysics`FeynArts`Incoming,
   HighEnergyPhysics`FeynArts`Outgoing,
   HighEnergyPhysics`FeynArts`External}*)};
    
(* ************************************************************** *)
